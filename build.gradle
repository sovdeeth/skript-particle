import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group 'com.sovdee'
project.ext.jomlVersion = "1.10.5"

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url 'https://repo.skriptlang.org/releases'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
    compileOnly("com.github.SkriptLang:Skript:2.12.0")
    implementation "org.joml:joml:${jomlVersion}"
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.property("version")]
    from("lang/") {
        include '*.lang'
        into 'lang/'
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    options.encoding = 'UTF-8'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.register('copyJar', Copy) {
    dependsOn jar
    from "build/libs/skript-particle.jar"
    into "e:/PaperServer/plugins"
}
